class MerchantStore play
{
	Array<StoreItem> StoreItems;
	Array<string> Categories;
}

extend class MerchantHandler
{
	Array<name> SafeMapNames;
	Array<StoreCurrency> StoreCurrencies;
	Array<StoreItem> StoreItems;

	MerchantStore Stores[3];
	int OpenedStoreIndex[MAXPLAYERS];

	static const name merchChances[] = { 'hdm_chance_weapon', 'hdm_chance_ammo', 'hdm_chance_item' };
	
	// Initialize Store of given type for the current map
	private void InitStore(int type) {
		// Ensure Store Exists
		if (!Stores[type]) Stores[type] = new('MerchantStore');
		
		// Clean current categories & items
		Stores[type].Categories.Clear();
		Stores[type].StoreItems.Clear();

		forEach (item : StoreItems) {

			// If the store item isn't valid, skip.
			if (
				!HDCore.CheckClassExists(item.GetItem())
				|| !item.IsEnabled()
				|| item.GetType() != type
			) continue;

			bool inSafeMap = false;
			forEach (mapName : SafeMapNames) {
				if (level.mapName == mapName) {
					inSafeMap = true;
					break;
				}
			}

			let chance = item.GetChance();
			if (inSafeMap || frandom[merchrand](0.0, 1.0) < chance) {
				item.StoreAmount = ceil(item.GetStoreAmount() * (inSafeMap ? random[merchrand](25, 150) : max(chance, 1.0)));

				Stores[type].StoreItems.Push(item);
			}
		}

		// [Ace] Don't duplicate categories.
		forEach (currItem : Stores[type].StoreItems) {
			if (Stores[type].Categories.Find(currItem.GetCategory()) == Stores[type].Categories.Size()) {
				Stores[type].Categories.Push(currItem.GetCategory());
			}
		}
	}

	// Get & add all class-defined store items
	private void InitStores() {
		forEach (cls : AllClasses) {
			if (HDCore.IsChildClass(cls, 'StoreItem')) {
				let item = StoreItem(new(cls));

				if (HDCore.CheckClassExists(item.GetItem())) StoreItems.push(item);
				else item.Destroy();
			}
		}
	}

	// Get & add all class-defined store currencies
	private void InitCurrencies() {
		forEach (cls : AllClasses) {
			if (HDCore.IsChildClass(cls, 'StoreCurrency')) {
				let currency = StoreCurrency(new(cls));

				if (HDCore.CheckClassExists(currency.GetCurrencyClass())) StoreCurrencies.Push(currency);
				else currency.Destroy();
			}
		}
	}
}